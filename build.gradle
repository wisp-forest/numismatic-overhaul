plugins {
    id 'fabric-loom' version '0.11-SNAPSHOT'
    id 'maven-publish'
    id 'io.github.juuxel.loom-quiltflower' version '1.6.0'
}

version = "${project.mod_version}+${project.minecraft_base_version}"
group = project.maven_group

repositories {
    maven   { url "https://maven.terraformersmc.com/" }
    maven   { url = "https://ladysnake.jfrog.io/artifactory/mods" }
    maven   { url = "https://maven.shedaniel.me/" }
    maven   { url = "https://maven.wispforest.io/" }
    maven   { url = "https://storage.googleapis.com/devan-maven/" }
    flatDir { dir("libs") }

    mavenLocal()
}

dependencies {
    minecraft "com.mojang:minecraft:${project.minecraft_version}"
    mappings "net.fabricmc:yarn:${project.yarn_mappings}:v2"
    modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"

    // Fabric API
    modImplementation "net.fabricmc.fabric-api:fabric-api:${project.fabric_version}"

    def noFabric = {
        exclude group: 'net.fabricmc.fabric-api'
    }

    modImplementation include("dev.onyxstudios.cardinal-components-api:cardinal-components-base:${project.cca_version}"), noFabric
    modImplementation include("dev.onyxstudios.cardinal-components-api:cardinal-components-entity:${project.cca_version}"), noFabric

    // REI
    modCompileOnly "me.shedaniel:RoughlyEnoughItems-api-fabric:${project.rei_version}"
    modLocalRuntime "me.shedaniel:RoughlyEnoughItems-fabric:${project.rei_version}"

    // DevLogin
    modLocalRuntime "com.ptsmods:devlogin:1.2.1", noFabric

    // Stacc
    modLocalRuntime include("net.devtech:Stacc:1.3.0"), noFabric

    // ModMenu
    modImplementation "com.terraformersmc:modmenu:${project.modmenu_version}"

    // oÏ‰o
    modImplementation "io.wispforest:owo-lib:${project.owo_version}"
    modCompileOnly include("io.wispforest:owo-sentinel:${project.owo_version}")

    // inventorio
    modImplementation ("me.lizardofoz.inventorio:inventorio-1.18-fabric-1.7.0")
}

processResources {
    inputs.property "version", project.version
    filteringCharset "UTF-8"
    filesMatching("fabric.mod.json") {
        expand "version": project.version
    }
}

def targetJavaVersion = 17

tasks.withType(JavaCompile).configureEach {
    // ensure that the encoding is set to UTF-8, no matter what the system default is
    it.options.encoding = "UTF-8"
    if (targetJavaVersion >= 10 || JavaVersion.current().isJava10Compatible()) {
        it.options.release = targetJavaVersion
    }
}

java {
    def javaVersion = JavaVersion.toVersion(targetJavaVersion)
    if (JavaVersion.current() < javaVersion) {
        toolchain.languageVersion = JavaLanguageVersion.of(targetJavaVersion)
    }
    archivesBaseName = project.archives_base_name
    // Loom will automatically attach sourcesJar to a RemapSourcesJar task and to the "build" task
    // if it is present.
    // If you remove this line, sources will not be generated.
    withSourcesJar()
}

jar {
    from("LICENSE") {
        rename { "${it}_${project.archivesBaseName}" }
    }
}
